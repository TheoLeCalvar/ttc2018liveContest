plugins {
  id "org.xtext.xtend" version "2.0.4"
}

xtext.version = "2.17.0"

repositories {
	mavenCentral()

	// for ATL
	maven { url "https://repo.eclipse.org/content/groups/releases/" }

	flatDir {
		dirs	'libs',				// for atol.jar
//			"../EMFSolutionTemplate",	// for solution.jar that contains the metamodel classes; TODO: change into project dependency?
							// => does not work because EMFSolutionTemplate/solution.jar contains EMF plugins without corresponding plugin.properties

			// TODO: change the two following into a project dependency?
			"../EMFSolutionAOF/libs",	// for aof.jar
			"../EMFSolutionAOF/build/libs"	// for EMFSolutionAOF.jar
	}
}

configurations {
	emftvm_compile
}

	dependencies {
		compile name: 'aof'
		compile name: 'EMFSolutionAOF'
		compile 'org.eclipse.xtend:org.eclipse.xtend.lib:2.17.0'
		compile name: 'atol'

		// could be compileOnly for ATOL, but must be compile for ATL
 		compile('org.eclipse.m2m.atl:org.eclipse.m2m.atl.emftvm.compiler:4.0.0-1') {
			exclude group: 'org.eclipse.m2m.atl', module: 'org.eclipse.m2m.atl.emfvm'	// ignore bad dependency in eclipse repository
		}

		emftvm_compile('org.eclipse.m2m.atl:org.eclipse.m2m.atl.emftvm.ant.standalone:4.0.0-1')
	}

apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'application'

apply plugin: 'java-library-distribution'

xtend {
	sourceSets {
		main.xtendOutputDir = 'xtend-gen'
	}
}

mainClassName = 'ttc2018.LiveContestDriver'

sourceSets {
	main {
		java {
			srcDirs = ['src', 'src-gen']
		}
		resources {
			srcDirs = ['src']
		}
	}
}

sourceSets {
	main {
		output.resourcesDir = "build/classes/main"
	}
	test {
		output.resourcesDir = "build/classes/test"
	}
}

task compile {
	doLast {
		ant.taskdef(name: 'emftvm_compile',
			classname: 'org.eclipse.m2m.atl.emftvm.ant.CompileTask',
			classpath: configurations.emftvm_compile.asPath)
		ant.emftvm_compile(module: 'Q1', modulepath: 'src/ttc2018/', outputpath: 'build/resources/main/ttc2018/', charset: 'UTF-8')
		ant.emftvm_compile(module: 'Q2', modulepath: 'src/ttc2018/', outputpath: 'build/resources/main/ttc2018/', charset: 'UTF-8')
	}
}

